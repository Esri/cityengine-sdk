//
// Esri CityEngine SDK Maya Plugin Example
//
// This example demonstrates the main functionality of the Procedural Runtime API.
// Esri R&D Center Zurich, Switzerland
//
// See http://github.com/ArcGIS/esri-cityengine-sdk for instructions.

proc string[] appendAarray(string $a0[], string $a1[]) {
	string $result[] = $a0;
	for($e in $a1)
		$result[size($result)] = $e;
	return $result;
}

proc string[] collectInitialShapes(string $nodes[]) {
	string $result[];
	for($node in $nodes) {
	    string $type = `nodeType $node`;
		if($type == "mesh") {
			$result = appendAarray($result, {$node});
		} else if($type == "transform") {
			string $tmp[] = collectInitialShapes(`listRelatives -ni -pa -children $node`);
			$result = appendAarray($result, $tmp);
		}
	}
	return $result;	  
}

global proc createPrtNode() {
	string $select[] = `ls -sl`;
	string $initialShapes[] = collectInitialShapes($select);
	
	if(!(size($initialShapes))) {
		confirmDialog -title "Missing selection" -message "Please select shapes first in order to apply a rule." -button "OK";
		return;
	}

	$filters = "Rule Package (*.rpk);;All Files (*.*)";
	string $rulePackage[] = `fileDialog2 -fm 1 -cap "Select Rule Package" -ff $filters`;
	
	
	if(size($rulePackage)) {
		for($node in $initialShapes) {
			select -r $node;
			prtAssign $rulePackage;
		}
	}
	select -r $select;
}

global proc createPrtMaterialNode() {
	string $select[] = `ls -sl`;
	string $initialShapes[] = collectInitialShapes($select);
	
	if(!(size($initialShapes))) {
		confirmDialog -title "Missing selection" -message "Please select shapes first in order to create materials." -button "OK";
		return;
	}	
	
	for($node in $initialShapes) {
		$prtMaterialName = `createNode prtMaterial`;
		$im =  `connectionInfo -sfd ($node +".inMesh")`;
		connectAttr -force ( $prtMaterialName + ".outMesh" ) ( $node + ".inMesh" );
		connectAttr -force ( $im ) ( $prtMaterialName + ".inMesh" );
	}
}

global proc createPrtMenu() {
	global string $gMainWindow;
	global string $gPrtMenu = "prtMenu"; 

	setParent $gMainWindow;
	if (`isTrue "BaseMayaExists"`) { 
		menu -l "PRT" -aob true -to true -postMenuCommandOnce true $gPrtMenu;
		setParent -m ..; 
		setParent -m $gPrtMenu;
		menuItem -divider true;
		menuItem -label "Attach Rule..."  -c "createPrtNode" -annotation "Attach a CGA rule package to a geometry";
		menuItem -label "Create Materials"  -c "createPrtMaterialNode" -annotation "Create Materials";
		setParent -m ..;
	}
}

global proc prt4mayaCreateUI() {
 	createPrtMenu();
}

// --- material helpers

global proc prtSetDiffuseTexture(string $sgName, string $texturePath, string $uvSet) {
	string $shName = $sgName + "H";
	shadingNode -asShader lambert -n $shName;
	connectAttr -f ($shName + ".outColor") ($sgName + ".surfaceShader");

	string $nodeName = $sgName + "F";

	shadingNode -asTexture file -n $nodeName;
	connectAttr -force ($nodeName + ".outColor") ($shName + ".color");
	setAttr ($nodeName + ".fileTextureName") -type "string" $texturePath;
}

global proc prtSetColor(string $sgName, float $r, float $g, float $b) {
	$shaderPlug = `connectionInfo -sfd ($sgName + ".surfaceShader")`;
	$shName = `plugNode $shaderPlug`;

	string $nodeName = "prtColor_" + $sgName;

	shadingNode -asShader blinn -n $nodeName;
	setAttr ($nodeName + ".color") -type "double3" $r $g $b;
	connectAttr -force ($nodeName + ".outColor") ($sgName + ".surfaceShader");
}
